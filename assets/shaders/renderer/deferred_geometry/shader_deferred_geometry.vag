//this shader is now exclusive for chunk meshes
#version 330 core
layout (location=0) in uint attributes;

out VS_OUT{
    vec2 TexCoords;
    //vec3 viewDir;//view direction in tangent space
	float ao;
    mat3 tangentToView;
}vs_out;

uniform mat4 model;
uniform mat4 view;
uniform mat4 projection;
uniform mat3 view_normal;

const vec3 normals[6] = vec3[](
	vec3(0,0,1),
	vec3(1,0,0),
	vec3(0,0,-1),
	vec3(-1,0,0),
	vec3(0,1,0),
	vec3(0,-1,0)
);

const vec3 tangents[6] = vec3[](
	vec3(1,0,0),
	vec3(0,0,-1),
	vec3(-1,0,0),
	vec3(0,0,1),
	vec3(1,0,0),
	vec3(1,0,0)
);

const vec3 bitangents[6] = vec3[](
	vec3(0,1,0),
	vec3(0,1,0),
	vec3(0,1,0),
	vec3(0,1,0),
	vec3(0,0,-1),
	vec3(0,0,1)
);

const float aoValues[4]=float[](0.1,0.25,0.5,1.0);

void main()
{    
	//unpack data
	uint data=attributes;
	uint id, uvx, uvy, posx, posy, posz, ao;

	id=data&7u;		data>>=3u;
	uvy=data&15u;	data>>=4u;
	uvx=data&15u;	data>>=4u;
	posz=data&63u;	data>>=6u;
	posy=data&63u;	data>>=6u;
	posx=data&63u;	data>>=6u;
	ao=data&3u;

	//everything else
	vs_out.TexCoords=0.1*vec2(uvx, uvy);
	vs_out.ao=aoValues[ao];
	
    vs_out.tangentToView=view_normal*mat3(tangents[id], bitangents[id], normals[id]);

    vec4 position=view*model*vec4(posx, posy, posz, 1);

    //vs_out.viewDir=-(transpose(vs_out.tangentToView)*position.xyz);
    gl_Position=projection*position;
}  